apply plugin: 'com.android.application'
apply from: "deploy.gradle"


project.ext.set("archivesBaseName", "reversatile")


android {


    compileSdk 34
    buildToolsVersion '34.0.0'

    useLibrary 'android.test.base'
    useLibrary 'android.test.mock'

    defaultConfig {
        multiDexEnabled false
        versionName androidGitVersion.name()
        versionCode androidGitVersion.code()
        ndk {
            abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
        }
        minSdkVersion 21
        targetSdkVersion 34
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
        }

        release {
            signingConfig null
        }

    }
    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES', 'META-INF/LICENSE', 'META-INF/LICENSE.txt', 'META-INF/license.txt', 'META-INF/NOTICE', 'META-INF/NOTICE.txt', 'META-INF/notice.txt', 'META-INF/ASL2.0', 'README.txt']
        }
        jniLibs.useLegacyPackaging = false
    }

    ndkVersion "23.1.7779620"

    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'

        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "reversatile-${variant.name}-${variant.versionName}${variant.signingReady? "-signed" : "-unsigned"}.apk"
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
    lint {
        disable 'GoogleAppIndexingWarning', 'UnusedAttribute'
    }
    namespace 'de.earthlingz.oerszebra'
    testNamespace 'de.earthlingz.oerszebra.tests'


}

repositories {
    mavenCentral()
    google()
    maven { url "https://jitpack.io" }
}

dependencies {
    api 'com.google.guava:guava:27.1-android'

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.core:core:1.12.0'
    implementation 'androidx.vectordrawable:vectordrawable-animated:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.github.matomo-org:matomo-sdk-android:4.1.4'

    implementation 'com.github.Innovattic:range-seek-bar:v1.0.6'
    implementation(platform("org.jetbrains.kotlin:kotlin-bom:1.8.0"))

    // Required -- JUnit 4 framework
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    // Optional -- Mockito framework
    testImplementation 'org.mockito:mockito-core:2.28.2'
    androidTestImplementation 'androidx.test:core:1.5.0'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    // Optional -- Hamcrest library
    androidTestImplementation 'org.hamcrest:hamcrest-library:2.1'
}
